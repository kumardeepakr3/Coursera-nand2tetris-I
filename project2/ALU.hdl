// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Put you code here:
    // 0000000000000000 bus
    And16(a=true, b=false, out=zero16Bus);

    // Till Negation x 
    Mux16(a=x, b=zero16Bus, sel=zx, out=zeroOrX);
    Not16(in=zeroOrX, out=notX);
    Mux16(a=zeroOrX, b=notX, sel=nx, out=afterNegX);
    
    // Till Negation y 
    Mux16(a=y, b=zero16Bus, sel=zy, out=zeroOrY);
    Not16(in=zeroOrY, out=notY);
    Mux16(a=zeroOrY, b=notY, sel=ny, out=afterNegY);

    Add16(a=afterNegX, b=afterNegY, out=xPlusY);
    And16(a=afterNegX, b=afterNegY, out=xAndY);

    Mux16(a=xAndY, b=xPlusY, sel=f, out=prevOut);
    Not16(in=prevOut, out=notPrevOut);

    Mux16(a=prevOut, b=notPrevOut, sel=no, out=outCopy);

    // Or of all the bits in outCopy
    Or16Way(in=outCopy, out=notZr);
    Not(in=notZr, out=zr);

    // Calculates the most significant bit in 'in' bus 
    MSB16(in=outCopy, out=mostSignificantBit);

    Mux(a=false, b=true, sel=mostSignificantBit, out=ng);

    // Nothing. Just setting out to outCopy
    And16(a=outCopy, b=outCopy, out=out);
}